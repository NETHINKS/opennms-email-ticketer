/************************************************************
* e-mail ticketer
*
* The e-mail ticketer uses the OpenNMS ticket integration
* for creating tickets in custom ticket systems by sending 
* e-mails.
*
* @author Michael Batz <michael.batz@nethinks.com>
*************************************************************/

package org.opennms.netmgt.correlation.drools;

import java.util.List;
import org.opennms.javamail.JavaMailer;
import org.opennms.javamail.JavaMailerException;
import org.opennms.netmgt.xml.event.Event;
import org.opennms.netmgt.xml.event.Parm;
import org.opennms.netmgt.model.OnmsAlarm;
import org.opennms.netmgt.model.OnmsNode;
import org.opennms.netmgt.dao.api.AlarmRepository;
import org.opennms.netmgt.dao.api.NodeDao;
import org.opennms.core.spring.BeanUtils

global java.lang.String MAIL_RECEIVER;
global java.lang.String MAIL_SUBJECT;
global org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine engine;


rule "ticket create event"
    when
        $e : Event( uei  == "uei.opennms.org/troubleTicket/create" )
    then
        // create ticket
        createTicket($e, MAIL_RECEIVER, MAIL_SUBJECT);

        // remove event from working memory
        retract($e);
end

function void createTicket(Event ticketEvent, String mailReceiver, String mailSubject)
{
    //get alarmId from Event
    String alarmId = getEventParmValue(ticketEvent, "alarmId");

    //get OnmsAlarm
    AlarmRepository repository = BeanUtils.getBean("daoContext", "alarmRepository", AlarmRepository.class);
    OnmsAlarm alarm = repository.getAlarm(new Integer(alarmId));
    if(alarm == null)
    {
        System.out.println("Drools ticketer: Could not find alarm " + alarmId);
        return;
    }

    //get nodeLabel, if node exists
    String nodeLabel = "";
    if(alarm.getNodeId() != null)
    {
        OnmsNode node = BeanUtils.getBean("daoContext", "nodeDao", NodeDao.class).get(new Long(alarm.getNodeId()).intValue());
        if(node != null)
        {
            nodeLabel = node.getLabel();
        }
    }

    // create message text
    String message;
    message = "OpenNMS Alarm";
    message += "Alarm ID: " + alarm.getId() + "\n";
    message += "Alarm Node: " + nodeLabel + "\n";
    message += "Alarm Message: " + alarm.getLogMsg() + "\n";

    //send e-mail
    sendMail(mailReceiver, mailSubject, message);
}



/**
* Gets an event parameter value
* @return String value of the event parameter, or an empty string if key does not exist
*/
function String getEventParmValue(Event event, String key)
{
    //prepare output
    String result = "";

    //go through all parms
    List<Parm> eventparms = event.getParmCollection();
    for(Parm parm : eventparms )
    {
        if(parm.getParmName().equals(key))
        {
            result = parm.getValue().getContent();
        }
    }

    return result;
}


function void sendMail(String receiver, String subject, String message)
{
    // create e-mail
    JavaMailer jm = new JavaMailer();
    jm.setTo(receiver);
    jm.setSubject(subject);
    jm.setMessageText(message);

    // send mail
    System.out.println(message);
    try
    {
        jm.mailSend();
    }
    catch(JavaMailerException e)
    {
        System.out.println("Drools ticketr: Error creating e-mail ticket");
    }
}
